buildscript { //TODO Remove
    // Done to not remap sources, since that causes the Forge subproject to crash out
    System.setProperty("fabric.loom.ci", true as String)
}

apply from: "https://raw.githubusercontent.com/SteveKunG/GradleScripts/main/architectury/forge.gradle"

loom {
    runs.configureEach {
        properties project.commonProperties
        vmArgs project.commonVmArgs
        vmArgs project.hotswapVmArgs
    }

    forge {
        mixinConfig("mixins.${archives_base_name}_forge.json")

        mixin {
            useLegacyMixinAp = true
            defaultRefmapName = "mixins.${archives_base_name}_forge.refmap.json"
        }
    }

    runs {
        client {
            if (project.hasProperty("usernameAndUUID")) {
                programArgs project.usernameAndUUID
            }
        }
        data {
            forgeTemplate("data")
            environment "data"
            programArgs "--mod=fishofthieves", "--all", "--output=" + file("src/generated/resources/"), "--existing=" + project(":common").file("src/main/resources/")
            name "Forge Data Generation"
        }
    }
}

repositories {
    maven {
        name "Aquaculture 2"
        url "https://girafi.dk/maven/"
        content {
            includeGroup "com.teammetallurgy.aquaculture"
        }
    }
}

dependencies {
    api "com.teammetallurgy.aquaculture:aquaculture2_1.19.4:${aquaculture_version}:api"
    compileOnly(annotationProcessor("io.github.llamalad7:mixinextras-common:${mixinextras_version}"))
    implementation(include("io.github.llamalad7:mixinextras-forge:${mixinextras_version}"))
}

configurations.modImplementation {
    exclude group: "me.shedaniel.cloth" //TODO Temporary exclude cloth-config
}

configurations.configureEach { //TODO Remove
    resolutionStrategy.dependencySubstitution {
        substitute module("cpw.mods:securejarhandler") using module("net.minecraftforge:securemodules:2.2.19")
    }
    resolutionStrategy.force("net.sf.jopt-simple:jopt-simple:5.0.4")
}

String fileName = "${archives_base_name}-mc${minecraft_version}"
List<String> commonExcludes = [".cache", "**/datagen/", "data/fabric/**", "architectury.common.json"]

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = "dev"
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
    excludes = commonExcludes
}

sourcesJar {
    Jar commonSources = project(":common").sourcesJar
    dependsOn commonSources
    commonSources.exclude ".cache"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from commonSources.archiveFile.map { zipTree(it) }
    excludes = commonExcludes
}

tasks.register("apiJar", Jar) {
    group "build"
    from project(":common").sourceSets.main.allJava
    from project(":common").sourceSets.main.output
    include "com/stevekung/fishofthieves/api/**/*"
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
    archiveClassifier = "api"
}

artifacts {
    archives apiJar
}

remapSourcesJar {
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge" + (release_type != "release" ? "-" + release_type : "")
    archiveVersion = "v${mod_version}"
    archiveBaseName = fileName
}