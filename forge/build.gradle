plugins {
    id "com.modrinth.minotaur"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    forge {
        mixinConfigs = ["mixins.fishofthieves.json", "mixins.fishofthieves_forge.json"]

        mixin {
            defaultRefmapName = "mixins.fishofthieves_forge.refmap.json"
        }

        dataGen {
            mod "fishofthieves"
        }
    }

    launches {
        if (project.hasProperty("username") && project.hasProperty("uuid")) {
            client {
                arg "--username", "${username}", "--uuid", "${uuid}"
            }
        }
        data {
            arg "--existing", project(":common").file("src/main/resources/").absolutePath
        }
    }

    runs {
        client {
            vmArg("-Dmixin.debug.export=true")
            vmArg("-Dmixin.hotSwap=true")
        }
        data {
            name "Forge Data Generation"
        }
    }
}

configurations {
    common
    shadowCommon
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

repositories {
    maven {
        name "Aquaculture 2"
        url "https://girafi.dk/maven/"
    }
}

dependencies {
    forge "net.minecraftforge:forge:${mc_version}-${forge_version}"

    api "com.teammetallurgy.aquaculture:aquaculture2_${mc_version}:${mc_version}-${aquaculture_version}:api"

    modRuntimeOnly "curse.maven:configured-457570:${configured_version}"
    modRuntimeOnly "curse.maven:catalogue-459701:${catalogue_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionForge")) { transitive false }
}

processResources {
    inputs.property "version", project.version

    filesMatching("META-INF/mods.toml") {
        expand "version": project.version
    }
}

String fileName = "${archives_base_name}-mc${mc_version}"

shadowJar {
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev"
    archiveVersion = "v${project.version}"
    archiveBaseName = fileName
    excludes = [".cache", "data/fabric/**"]
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    commonSources.exclude ".cache"
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    from commonSources.archiveFile.map { zipTree(it) }
    excludes = [".cache", "data/fabric/**"]
}

remapSourcesJar {
    archiveVersion = "v${project.version}"
    archiveBaseName = fileName
}

remapJar {
    inputFile.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = "forge"
    archiveVersion = "v${project.version}"
    archiveBaseName = fileName
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}

def uploadDebug = true

curseforge {
    apiKey = project.hasProperty("curseforge_api_token") ? project.curseforge_api_token : ""
    project {
        id = "568372"
        changelog = rootProject.file("CHANGE_LOG.md")
        changelogType = "markdown"
        releaseType = "beta"
        addGameVersion "Forge"
        addGameVersion "Java 17"
        addGameVersion project.mc_version

        mainArtifact(remapJar.archiveFile) {
            displayName = "[Forge] " + fileName + "-v${project.version}"
        }
        relations {
            requiredDependency "cloth-config"
            optionalDependency "configured"
        }
        afterEvaluate {
            uploadTask.dependsOn(remapJar)
        }
        addArtifact shadowJar
        addArtifact remapSourcesJar
    }
    options {
        debug uploadDebug
        forgeGradleIntegration = false
    }
}

modrinth {
    debugMode = uploadDebug
    token = project.hasProperty("modrinth_api_token") ? project.modrinth_api_token : ""
    projectId = "fish-of-thieves"
    changelog = rootProject.file("CHANGE_LOG.md").text
    versionName = "[Forge] Fish of Thieves " + "v${project.version}" + " for Minecraft " + project.mc_version
    versionNumber = project.mod_version + "-forge"
    versionType = "release"
    uploadFile = remapJar
    additionalFiles = [shadowJar, remapSourcesJar]
    gameVersions = ["${project.mc_version}"]
    loaders = ["forge"]
    dependencies {
        required.project "cloth-config"
        optional.project "configured"
    }
}